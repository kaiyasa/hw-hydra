// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
      Inc16(in=counter, out=next);
      Mux8Way16(sel[0]=inc, sel[1]=load, sel[2]=reset, out=value,
                a=counter,   // 000
                b=next,      // 001
                c=in,        // 010
                d=in,        // 011
                e=false,     // 100
                f=false,     // 101
                g=false,     // 110
                h=false      // 111
      );

      Or(a=load, b=reset, out=m0);
      Or(a=inc, b=m0, out=modify);
      Register(load=modify, in=value, out=counter, out=out);
}
